apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.glshop.net"
        minSdkVersion 8
        targetSdkVersion 14
        versionCode 29
        versionName "1.4"
        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "channel_name"]
    }
    //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
    lintOptions {
        abortOnError false
    }
    //签名
    signingConfigs {
        debug {
//            storeFile file("C:/Users/Xinxiwang/.android/debug.keystore")
            storeFile file("key/glshop.keystore")
            storePassword "glshop123"
            keyAlias "glshop"
            keyPassword "glshop123"
        }
        relealse {
            storeFile file("key/glshop.keystore")
            storePassword "glshop123"
            keyAlias "glshop"
            keyPassword "glshop123"
        }
    }

    buildTypes {
        release {
            //混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.relealse
        }
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            //versionName追加名字
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
    }
    productFlavors {

        gl {}

        yingyongbao {}

        anzhi {}

        huawei {}

        wandoujia {}

        anzhuobaidu {}

        shop360 {}

        xiaomi {}

        yingyonghui {}

        jifeng {}
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = outputFile.name.replace(".apk", "-${defaultConfig.versionName}.apk")
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile project(':gl_platform_sdk')
    compile project(':gl_platform_api')
    compile files('libs/libammsdk.jar')
}
